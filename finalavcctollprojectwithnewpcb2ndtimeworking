#define AXLE_SENSOR_PIN 2
#define HEIGHT_SENSOR_COUNT 10
#define VEHICLE_TIMEOUT 3000
#define AXLE_DEBOUNCE_MS 50

unsigned long axleCount = 0;
bool lastAxleState = HIGH;
unsigned long lastAxleChangeTime = 0;

const int heightSensorPins[HEIGHT_SENSOR_COUNT] = {3,4,5,6,7,8,9,10,11,12};
const int heightValues[HEIGHT_SENSOR_COUNT]     = {1400,1520,1650,1780,1910,2030,2400,2600,2800,3000};

bool lastHeightState[HEIGHT_SENSOR_COUNT];
int maxHeight = 0;

unsigned long lastDetectionTime = 0;

void setup() {
  Serial.begin(115200);
  Serial.println("System Ready!");

  pinMode(AXLE_SENSOR_PIN, INPUT_PULLUP);
  for (int i = 0; i < HEIGHT_SENSOR_COUNT; i++) {
    pinMode(heightSensorPins[i], INPUT_PULLUP);
    lastHeightState[i] = HIGH;
  }
}

void loop() {
  unsigned long currentMillis = millis();
   bool currentAxleState = digitalRead(AXLE_SENSOR_PIN);
  if (lastAxleState == HIGH && currentAxleState == LOW) {
    axleCount++;
    lastDetectionTime = millis();
    Serial.print("Axle detected. Count = ");
    Serial.println(axleCount);
    delay(30);
  }
  lastAxleState = currentAxleState;

  // ---------------- HEIGHT SENSORS ----------------
  for (int i = 0; i < HEIGHT_SENSOR_COUNT; i++) {
    bool currentState = digitalRead(heightSensorPins[i]);
    if (lastHeightState[i] == HIGH && currentState == LOW) {
      lastDetectionTime = currentMillis;
      if (heightValues[i] > maxHeight) {
        maxHeight = heightValues[i];
      }
      Serial.print("Height sensor triggered: ");
      Serial.print(i + 1);
      Serial.print(" | Height = ");
      Serial.println(heightValues[i]);
    }
    lastHeightState[i] = currentState;
  }

  // ---------------- VEHICLE TIMEOUT ----------------
  if (axleCount > 0 && (currentMillis - lastDetectionTime > VEHICLE_TIMEOUT)) {
    classifyVehicle(axleCount, maxHeight);
    axleCount = 0;
    maxHeight = 0;
  }
}

void classifyVehicle(unsigned long axles, int height) {
  String type;

  if (axles == 2 && height < 1600) type = "Car";
  else if (axles == 2 && height >= 1600 && height < 2200) type = "Van/Jeep";
  else if (axles == 2 && height >= 2200) type = "Bus";
  else if (axles == 3) type = "Mini-Truck";
  else if (axles >= 4 && height >= 2500) type = "Heavy Truck";
  else type = "Unknown";

  Serial.print("|AA|");
  Serial.print(axles);
  Serial.print("|");
  Serial.print(type);
  Serial.print("|");
  Serial.print(height);
  Serial.println("|FF|");

  //Serial.println("----- Vehicle Passed -----");
  //Serial.print("Total axles: "); Serial.println(axles);
  //Serial.print("Max height: "); Serial.println(height);
  //Serial.print("Vehicle type: "); Serial.println(type);
  //Serial.println();
}
